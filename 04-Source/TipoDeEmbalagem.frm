VERSION 5.00
Begin VB.Form frmTipoDeEmbalagem 
   BorderStyle     =   1  'Fixed Single
   Caption         =   "Tipo de Embalagem"
   ClientHeight    =   1695
   ClientLeft      =   1650
   ClientTop       =   2145
   ClientWidth     =   6480
   Icon            =   "TipoDeEmbalagem.frx":0000
   LinkTopic       =   "Form1"
   MaxButton       =   0   'False
   MDIChild        =   -1  'True
   PaletteMode     =   1  'UseZOrder
   ScaleHeight     =   1695
   ScaleWidth      =   6480
   Begin VB.CommandButton cmdCancelar 
      Cancel          =   -1  'True
      Caption         =   "&Cancelar"
      Height          =   345
      Left            =   5205
      TabIndex        =   7
      Top             =   1320
      Width           =   1245
   End
   Begin VB.CommandButton cmdGravar 
      Caption         =   "&Gravar"
      Height          =   345
      Left            =   3885
      TabIndex        =   6
      Top             =   1320
      Width           =   1245
   End
   Begin VB.Frame frEmbalagem 
      Height          =   1275
      Left            =   0
      TabIndex        =   3
      Top             =   0
      Width           =   6465
      Begin VB.TextBox txtAbreviado 
         Height          =   285
         Left            =   5220
         TabIndex        =   1
         Top             =   300
         Width           =   975
      End
      Begin VB.TextBox txtDescrição 
         Height          =   285
         Left            =   1200
         MaxLength       =   30
         TabIndex        =   2
         Top             =   750
         Width           =   5000
      End
      Begin VB.TextBox txtCódigo 
         Height          =   285
         Left            =   1200
         TabIndex        =   0
         Top             =   300
         Width           =   750
      End
      Begin VB.Label lblDescrição 
         Caption         =   "Descrição"
         Height          =   195
         Left            =   150
         TabIndex        =   5
         Top             =   780
         Width           =   885
      End
      Begin VB.Label lblCódigo 
         Caption         =   "Código"
         Height          =   200
         Left            =   150
         TabIndex        =   4
         Top             =   330
         Width           =   855
      End
   End
End
Attribute VB_Name = "frmTipoDeEmbalagem"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Explicit

Dim TBLTipoDeEmbalagem As Table
Dim TipoDeEmbalagemAberto As Boolean
Dim IndiceTipoDeEmbalagemAtivo$

Dim lAllowInsert  As Boolean
Dim lAllowEdit    As Boolean
Dim lAllowDelete  As Boolean
Dim lAllowConsult As Boolean

Dim lPula As Boolean
Dim lInserir As Boolean
Dim lAlterar As Boolean
Dim mFechar As Boolean

Dim StatusBarAviso$

Dim DataBaseName(1 To 1) As String
Public Relatório$
Public TotalDatabaseName%

Public lAtualizar As Boolean
Private Sub AtivaCampos()
    BotãoImprimir True
    frEmbalagem.Enabled = True
    BotãoGravar (lInserir Or lAllowEdit)
    cmdCancelar.Enabled = (lInserir Or lAllowEdit)
    cmdGravar.Enabled = (lInserir Or lAllowEdit)
End Sub
Private Function Cancelamento()
    Dim Confirmação%, Espaços%, Msg1$, Msg2$
    
    Msg1 = "Você está preste a cancelar a operação que esta realizando !"
    Msg2 = "Tem certeza?"
    Espaços = ((Len(Msg1) - Len(Msg2)) / 2) + 4
    Msg2 = String(Espaços, " ") + Msg2
    Confirmação = MsgBox(Msg1 + vbCr + Msg2, vbYesNo + vbQuestion + vbDefaultButton2, "Confirmação")
    
    If Confirmação = vbNo Then
        Cancelamento = False
        Exit Function
    End If
    
    If lInserir Then
        StatusBarAviso = "Inclusão cancelada"
    End If
    If lAlterar Then
        StatusBarAviso = "Alteração cancelada"
    End If
    BarraDeStatus StatusBarAviso
    
    lInserir = False
    lAlterar = False
    BotãoIncluir lAllowInsert
    
    If TBLTipoDeEmbalagem.RecordCount = 0 Then
        NavegaçãoInferior False
        NavegaçãoSuperior False
        BotãoGravar False
        cmdGravar.Enabled = False
        cmdCancelar.Enabled = False
        DesativaCampos
        ZeraCampos
        Cancelamento = True
        Exit Function
    End If
    
    Cancelamento = True
    
    TestaInferior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    TestaSuperior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    
    GetRecords
End Function
Private Sub DesativaCampos()
    BotãoImprimir False
    frEmbalagem.Enabled = False
    cmdGravar.Enabled = False
    cmdCancelar.Enabled = False
    BotãoGravar False
End Sub
Public Sub Encontrar()
    If Not lAllowConsult Then
        Exit Sub
    End If
    Set frmEncontrar.DBBancoDeDados = DBCadastro
    frmEncontrar.NomeDaJanela = "Tipo de Embalagem"
    frmEncontrar.LabelDescription = "Descrição"
    frmEncontrar.Mensagem = "Nenhum Tipo de Embalagem foi selecionado!"
    frmEncontrar.BancoDeDados = "CADASTRO"
    frmEncontrar.Tabela = "TIPO DE EMBALAGEM"
    frmEncontrar.Indice = "2"
    frmEncontrar.CampoChave = "CÓDIGO"
    frmEncontrar.CampoPreencheLista = "DESCRIÇÃO"
    frmEncontrar.Show vbModal
    lPula = True
    txtCódigo = frmEncontrar.Chave
    lPula = False
    PosRecords
End Sub
Public Sub Excluir()
    Dim Confirmação As Integer, Msg1$, Msg2$

    If lAlterar Then
       If Not Cancelamento Then
            Exit Sub
        End If
    End If

    StatusBarAviso = "Exclusão"
    BarraDeStatus StatusBarAviso
    
    Msg1 = "Você está preste a apagar um registro !"
    Msg2 = "Tem certeza?"
    Msg2 = String(((Len(Msg1) - Len(Msg2)) / 2), " ") + Msg2
    Confirmação = MsgBox(Msg1 + vbCr + Msg2, vbYesNo + vbQuestion + vbDefaultButton2, "Confirmação")
    
    If Confirmação = vbNo Then
        Exit Sub
    End If
    
    WS.BeginTrans
    
    TBLTipoDeEmbalagem.Delete
    
    If Err <> 0 Then
        GeraMensagemDeErro "TipoDeEmbalagem - Excluir", True
        StatusBarAviso = "Falha na exclusão"
        BarraDeStatus StatusBarAviso
        Exit Sub
    End If
    
    WS.CommitTrans
    
    Log gUsuário, "Exclusão - Tipo de Embalagem: " & txtCódigo & " - " & txtDescrição
    
    StatusBarAviso = "Exclusão bem sucedida"
    BarraDeStatus StatusBarAviso
    
    If TBLTipoDeEmbalagem.RecordCount = 0 Then
        NavegaçãoInferior False
        NavegaçãoSuperior False
        BotãoExcluir False
        BotãoGravar False
        cmdGravar.Enabled = False
        cmdCancelar.Enabled = False
        DesativaCampos
        ZeraCampos
        Exit Sub
    End If
    
    If TBLTipoDeEmbalagem.BOF Then
        TBLTipoDeEmbalagem.MoveFirst
    ElseIf TBLTipoDeEmbalagem.EOF Then
        TBLTipoDeEmbalagem.MoveLast
    Else
        TBLTipoDeEmbalagem.MovePrevious
        If TBLTipoDeEmbalagem.BOF Then
            TBLTipoDeEmbalagem.MoveNext
        End If
    End If
    
    GetRecords
    
    TestaInferior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    TestaSuperior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
End Sub
Public Sub Gravar()
    If lInserir Then
        If SetRecords Then
            PosRecords
            lInserir = False
            StatusBarAviso = "Inclusão bem sucedida"
        Else
            StatusBarAviso = "Falha na inclusão"
        End If
    Else
        If TBLTipoDeEmbalagem.RecordCount > 0 And Not TBLTipoDeEmbalagem.BOF And Not TBLTipoDeEmbalagem.EOF Then
            If SetRecords Then
                PosRecords
                lAlterar = False
                StatusBarAviso = "Alteração bem sucedida"
            Else
                StatusBarAviso = "Falha na alteração"
            End If
        End If
    End If
    
    BarraDeStatus StatusBarAviso
    
    TestaInferior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    TestaSuperior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    
    If TBLTipoDeEmbalagem.RecordCount = 0 Then
        If Not lInserir And Not lAlterar Then
            BotãoExcluir False
            BotãoGravar False
            cmdGravar.Enabled = False
            cmdCancelar.Enabled = False
        End If
    Else
        BotãoExcluir lAllowDelete
    End If
    
    BotãoIncluir lAllowInsert
    
    If txtCódigo.Enabled Then
        txtCódigo.SetFocus
    End If
End Sub
Public Sub Incluir()
    If lAlterar Then
       If Not Cancelamento Then
            Exit Sub
        End If
    End If
    
    lInserir = True
    
    ZeraCampos
    AtivaCampos
    
    BotãoGravar (lInserir Or lAllowEdit)
    BotãoIncluir False
    cmdGravar.Enabled = (lInserir Or lAllowEdit)
    cmdCancelar.Enabled = (lInserir Or lAllowEdit)
    
    NavegaçãoInferior False
    NavegaçãoSuperior False
    
    StatusBarAviso = "Inclusão"
    BarraDeStatus StatusBarAviso
    
    txtCódigo.SetFocus
End Sub
Public Sub MoveFirst()
    If lAlterar Then
       If Not Cancelamento Then
            Exit Sub
        End If
    End If
    
    StatusBarAviso = "Pronto"
    BarraDeStatus StatusBarAviso
    
    TBLTipoDeEmbalagem.MoveFirst
    
    NavegaçãoInferior False
    NavegaçãoSuperior lAllowConsult
    
    GetRecords
End Sub
Public Sub MoveLast()
    If lAlterar Then
       If Not Cancelamento Then
            Exit Sub
        End If
    End If
    
    StatusBarAviso = "Pronto"
    BarraDeStatus StatusBarAviso
    
    TBLTipoDeEmbalagem.MoveLast
    
    NavegaçãoInferior lAllowConsult
    NavegaçãoSuperior False
    
    GetRecords
End Sub
Public Sub MoveNext()
    If lAlterar Then
       If Not Cancelamento Then
            Exit Sub
        End If
    End If
    
    TBLTipoDeEmbalagem.MoveNext
    If TBLTipoDeEmbalagem.EOF Then
        TBLTipoDeEmbalagem.MovePrevious
        Exit Sub
    End If
    
    StatusBarAviso = "Pronto"
    BarraDeStatus StatusBarAviso
    
    NavegaçãoInferior lAllowConsult
    TestaSuperior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    
    GetRecords
End Sub
Public Sub MovePrevious()
    If lAlterar Then
       If Not Cancelamento Then
            Exit Sub
        End If
    End If
    
    TBLTipoDeEmbalagem.MovePrevious
    If TBLTipoDeEmbalagem.BOF Then
        TBLTipoDeEmbalagem.MoveNext
        Exit Sub
    End If
    
    StatusBarAviso = "Pronto"
    BarraDeStatus StatusBarAviso
    
    NavegaçãoSuperior lAllowConsult
    TestaInferior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    
    GetRecords
End Sub
Public Sub PosRecords()
    If TBLTipoDeEmbalagem.RecordCount = 0 Then
        Exit Sub
    End If
    
    TBLTipoDeEmbalagem.Seek "=", txtCódigo
    If TBLTipoDeEmbalagem.NoMatch Then
        MsgBox "Não consegui encontrar " + txtCódigo, vbExclamation, "Erro"
        TBLTipoDeEmbalagem.MoveFirst
        NavegaçãoInferior False
        NavegaçãoInferior lAllowConsult
    Else
        TestaInferior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
        TestaSuperior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    End If
    GetRecords
End Sub
Private Sub GetRecords()
    On Error GoTo Erro
    
    If Not lAllowConsult Then
        ZeraCampos
        DesativaCampos
        Exit Sub
    End If
    txtCódigo = TBLTipoDeEmbalagem("CÓDIGO")
    txtDescrição = TBLTipoDeEmbalagem("DESCRIÇÃO")
    txtAbreviado = TBLTipoDeEmbalagem("ABREVIADO")
    If Not lAllowEdit Then
        DesativaCampos
    End If
    
    Exit Sub
    
Erro:
    GeraMensagemDeErro "Tipo de Embalagem - GetRecords "
    Resume Next
End Sub
Private Function SetRecords()
    On Error GoTo Erro
    
    Dim Msg$
    Dim Confirmação As Integer, Msg1$, Msg2$
    
    WS.BeginTrans 'Inicia uma Transação
    
    If lInserir Then
        TBLTipoDeEmbalagem.AddNew
    Else
        TBLTipoDeEmbalagem.Edit
    End If
    
    TBLTipoDeEmbalagem("CÓDIGO") = txtCódigo
    TBLTipoDeEmbalagem("DESCRIÇÃO") = txtDescrição
    TBLTipoDeEmbalagem("ABREVIADO") = txtAbreviado
    If lInserir Then
        TBLTipoDeEmbalagem("USERNAME - CRIA") = gUsuário
        TBLTipoDeEmbalagem("DATA - CRIA") = Date
        TBLTipoDeEmbalagem("HORA - CRIA") = Time
        TBLTipoDeEmbalagem("USERNAME - ALTERA") = "VAZIO"
        TBLTipoDeEmbalagem("DATA - ALTERA") = vbNull
        TBLTipoDeEmbalagem("HORA - ALTERA") = vbNull
    End If
    If lAlterar Then
        TBLTipoDeEmbalagem("USERNAME - ALTERA") = gUsuário
        TBLTipoDeEmbalagem("DATA - ALTERA") = Date
        TBLTipoDeEmbalagem("HORA - ALTERA") = Time
    End If
    TBLTipoDeEmbalagem.Update
        
Erro:
    If Err <> 0 Then
        TBLTipoDeEmbalagem.CancelUpdate
        GeraMensagemDeErro "TipoDeEmbalagem - SetRecords", True
        SetRecords = False
        Exit Function
    End If

    WS.CommitTrans 'Grava as alterações ou inclusões se não houverem erros
    
    If lInserir Then
        Log gUsuário, "Inclusão - Tipo de Embalagem: " & txtCódigo & " - " & txtDescrição
    Else
        Log gUsuário, "Alteração - Tipo de Embalagem: " & txtCódigo & " - " & txtDescrição
    End If
    
    SetRecords = True
End Function
Private Sub ZeraCampos()
    txtCódigo = Empty
    txtDescrição = Empty
    txtAbreviado = Empty
End Sub
Private Sub cmdCancelar_Click()
    Cancelamento
End Sub
Private Sub cmdGravar_Click()
    Gravar
End Sub
Private Sub Form_Activate()
    If mFechar Then
        Unload Me
        Exit Sub
    End If
    If Not TipoDeEmbalagemAberto Then
        Unload Me
        Exit Sub
    End If
    
    TestaInferior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    TestaSuperior TBLTipoDeEmbalagem, lAllowEdit, lAllowDelete, lAllowConsult
    
    If TBLTipoDeEmbalagem.RecordCount = 0 Then
        BotãoGravar False
        cmdGravar.Enabled = False
        cmdCancelar.Enabled = False
        BotãoImprimir False
    Else
        BotãoGravar (lInserir Or lAllowEdit)
        cmdGravar.Enabled = (lInserir Or lAllowEdit)
        cmdCancelar.Enabled = (lInserir Or lAllowEdit)
        BotãoImprimir True
    End If
    
    If lInserir Then
        BotãoGravar (lInserir Or lAllowEdit)
        cmdGravar.Enabled = (lInserir Or lAllowEdit)
        cmdCancelar.Enabled = (lInserir Or lAllowEdit)
        NavegaçãoInferior False
        NavegaçãoSuperior False
        BotãoExcluir False
        BotãoIncluir False
    ElseIf lAlterar Then
        BotãoIncluir lAllowInsert
    Else
        BotãoIncluir lAllowInsert
        StatusBarAviso = "Pronto"
    End If
    
    If lAtualizar Then
        BotãoAtualizar True
    Else
        BotãoAtualizar False
    End If
    
    BarraDeStatus StatusBarAviso
End Sub
Private Sub Form_Deactivate()
    cmdGravar.Enabled = False
    cmdCancelar.Enabled = False
    BotãoImprimir False
End Sub
Private Sub Form_Load()
    On Error GoTo Erro
    
    lAllowInsert = Allow("TIPO DE EMBALAGEM", "I")
    lAllowEdit = Allow("TIPO DE EMBALAGEM", "A")
    lAllowDelete = Allow("TIPO DE EMBALAGEM", "E")
    lAllowConsult = Allow("TIPO DE EMBALAGEM", "C")
    
    
    ZeraCampos
    
    lPula = False
    lInserir = False
    lAlterar = False
    
    TipoDeEmbalagemAberto = AbreTabela(Dicionário, "CADASTRO", "TIPO DE EMBALAGEM", DBCadastro, TBLTipoDeEmbalagem, TBLTabela, dbOpenTable)
    
    If TipoDeEmbalagemAberto Then
        IndiceTipoDeEmbalagemAtivo = "TIPODEEMBALAGEM1"
        TBLTipoDeEmbalagem.Index = IndiceTipoDeEmbalagemAtivo
    Else
        MsgBox "Não consegui abrir a tabela 'Tipo De Embalagem' !", vbCritical, "Erro"
        Exit Sub
    End If
    
    BotãoIncluir lAllowInsert
 
    If TBLTipoDeEmbalagem.RecordCount = 0 Then
        DesativaCampos
        BotãoExcluir False
        BotãoGravar False
    Else
        AtivaCampos
        BotãoExcluir lAllowDelete
        BotãoGravar (lInserir Or lAllowEdit)
        GetRecords
    End If
    
    NavegaçãoInferior False
        
    If TBLTipoDeEmbalagem.RecordCount = 0 Or TBLTipoDeEmbalagem.RecordCount = 1 Then
        NavegaçãoSuperior False
    Else
        NavegaçãoInferior lAllowConsult
    End If
    
    StatusBarAviso = "Pronto"
    Relatório = AddPath(AplicaçãoPath, "REPORT\TIPO DE EMBALAGEM.RPT")
    TotalDatabaseName = 1
    DataBaseName(1) = AddPath(AplicaçãoPath, "DATABASE\CADASTRO.MDB")
    mFechar = False
    Exit Sub
    
Erro:
    GeraMensagemDeErro "TipoDeEmbalagem - Load"
    mFechar = True
End Sub
Private Sub Form_QueryUnload(Cancel As Integer, UnloadMode As Integer)
    If lInserir Then
        MsgBox "Você está em uma inclusão!", vbExclamation, Caption
        StatusBarAviso = "Finalize a inclusão"
        BarraDeStatus StatusBarAviso
        Cancel = 1
        SetaFocus Me
        mdiGeal.Mostrar
        Exit Sub
    End If
    If lAlterar Then
        MsgBox "Você está em uma alteração!", vbExclamation, Caption
        StatusBarAviso = "Finalize a alteração"
        BarraDeStatus StatusBarAviso
        Cancel = 1
        SetaFocus Me
        mdiGeal.Mostrar
        Exit Sub
    End If
    
    mdiGeal.StatusBar.Panels("Posição").Visible = False
    ResizeStatusBar
    
    Set frmTipoDeEmbalagem = Nothing
End Sub
Private Sub Form_Unload(Cancel As Integer)
    If TipoDeEmbalagemAberto Then
        TBLTipoDeEmbalagem.Close
    End If
    If Forms.Count = 2 Then
        AllBotões False
    End If
End Sub
Private Sub txtCódigo_Change()
    If Not lPula Then
        FormatMask "9999", txtCódigo
    End If
End Sub
Private Sub txtCódigo_KeyPress(KeyAscii As Integer)
    If Not lInserir Then
        lAlterar = True
        StatusBarAviso = "Alteração"
        BarraDeStatus StatusBarAviso
    End If
End Sub
Private Sub txtCódigo_LostFocus()
    LeftBlank txtCódigo
End Sub
Private Sub txtDescrição_Change()
    If Not lPula Then
        FormatMask "@!S30", txtDescrição
    End If
End Sub
Private Sub txtDescrição_KeyPress(KeyAscii As Integer)
    If Not lInserir Then
        lAlterar = True
        StatusBarAviso = "Alteração"
        BarraDeStatus StatusBarAviso
    End If
End Sub
